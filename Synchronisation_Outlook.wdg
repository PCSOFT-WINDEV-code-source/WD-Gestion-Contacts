#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : Synchronisation_Outlook
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x18496330046b4ee9
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  internal_properties : CAAAAAgAAAA4aTX6qRYqwg1Om5gItW5pLv6ATr2WPF+QJ4Qr4JXDP1GQ5Hz80yFyNHsDECYWAZkK/62/iup0ekdIsREW68PVxP9Mdr/U
  type_code : 31
  p_codes :
   -
     code : |1+
      gnIDSession est un entier 
      
     type : 720896
  procedures :
   -
     name : GèreModifContact_PC_Outlook
     internal_properties : CAAAAAgAAAD33dHv0c15bcYajqGz+Y6e5i6wLpqotm+8vXu2nuMBl0xz75/tz13qdVwLut28inzpnCzDeEBfWHonvzAuy2SRmuwog6Og3xc+bT/IzSTGyuk4/9owwPr5oBeq9Si+BQ8jVYTx5jbmBbqFxRUP1BGimZus8pfUS1kAe2xWy8NGdUY+iHkkSnrBvUMX7zn6e9sW5rvya4DWdvQTWhAT46LDgQ17b3w1vm44S3v0hn8g1mlwj4s7Mt/bs+mRNOVJNUuDB/yyo3I+6U5ObSTeB7uYKCB3d/xLgLKo13wJI9cy7veVGRaAjqKGVBC5G+C7+z5TDZOFdnvlHzNOPJPoNfd6
     procedure_id : 1750041565078254800
     type_code : 15
     code : |1-
      // Résumé : Procédure qui gère les modifications d'un contact entre l'application et Outlook du PC s'il y en a eu
      // Syntaxe :
      //GèreModifContact_PC_Outlook ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      
      Procédure PRIVEE GèreModifContact_PC_Outlook()
      
      sListeDiff est une chaine	// Liste des différences entre le contact du PC et le contact de Outlook
      
      // Vérifie si le contact est identique sur le PC et sur Outlook
      // s'il y a eu des modifications : on les liste et on demande à l'utilisateur ce qu'il veut mettre à jour : base application ou Outlook
      SI bEstDifférentOutlookEtAppli(sListeDiff) = Vrai ALORS
      	
      	//1 : Conserver les valeurs de Outlook
      	//2 : Conserver les valeurs de l'application
      	//3 : Ne rien faire
      	SELON Dialogue(<§?18496C7F054CAB6A§>,  Contact.Nom + " " + contact.Prénom, sListeDiff)
      		
      		// Conserver les valeurs de Outlook
      		CAS 1 :
      			// Supprime le contact de l'application
      			HSupprime(Contact)						
      			// Ajoute le contact présent dans Outlook dans la base de l'application
      			CopieContactVersAppli()
      			
      		// Conserver les valeurs du PC
      		CAS 2 :
      			// Supprime le contact de Outlook
      			contactSupprime(gnIDSession)
      			// Ajoute le contact de l'application dans Outlook
      			CopieContactVersOutlook()
      			
      		//Ne rien faire
      		CAS 3 :
      			
      	FIN
      	
      FIN
     type : 458752
   -
     name : bEstDifférentOutlookEtAppli
     internal_properties : CAAAAAgAAABpFAQyUw3MD7JxMbcmgATOsF3vuDONAuH+120JjCsh9ROpRqGN7CvYzica5pk1KoZIKC3ncde6EHCvnWUI5h9FE0WCogazBpDUVOCCST98sAqV+EVE1Y32GUBurjnGwbsizeVm8GRn5GR2rOzX0YB0IbrMUoIt+ZLS3SIPyXvfGoLHCWXBJGQ+ftiJrRrgxcnCmVIaD+6nmACeorQLTdqrmaHQjSP8uA/6C9WntHrUkh6+r9afJH9mYj7tcAWujNXZRlJGAL728zYiDwsH75LUIW51W23CK9/LLt9pBLasdmanByR3yccxTZLyPYypaLb5hgjB74QQkUirddxSeAL1dG8Sz7y6pW9XH6EhurnW/SCcdJGoqrO/9lWLQGuu7VJrlFksgXEa9svnxc+ymC/HLIIKPvNpt9UazDBvM4xnu8oJ1Xb4gBXe6lTqZZpbOnSgk5EIsn1+sKWB
     procedure_id : 1750041732582018266
     type_code : 15
     code : |1-
      // Résumé : Teste les différences d'un contact entre sa version dans l'application et sa version dans Outlook du PC
      // Syntaxe :
      //[ <Résultat> = ] bEstDifférentOutlookEtAppli (<sListe>)
      //
      // Paramètres :
      //	sListe : Paramètre de retour contenant la liste des différences s'il en existe
      // Valeur de retour :
      // 	booléen : VRAI s'il existe des différences, FAUX autrement
      //
      
      Procédure PRIVEE bEstDifférentOutlookEtAppli(sListe)
      
      //Variables locales
      dhDate 					est un dateheure	
      dDate 					est une date
      sListeDifférences 		est une chaine
      sDatePCAffichee 		est une chaine	// Chaine affichant correctement une date de la base locale
      sDateOutlookAffichee 	est une chaine	// Chaine affichant correctement une date de Outlook
      
      // Nom
      SI (Contact.Nom 				<> mContact.Nom) ALORS 
      	sListeDifférences += ChaîneConstruit("Nom PC : %1, Nom Outlook : %2",Contact.Nom, mContact.Nom) + RC
      FIN
      
      // Prénom
      SI (Contact.Prénom				<> mContact.Prénom) ALORS 
      	sListeDifférences += ChaîneConstruit("Prénom PC : %1, Prénom Outlook : %2",Contact.Prénom, mContact.Prénom) + RC
      FIN
      
      // Prénom2
      SI (Contact.Prénom2				<> mContact.Prénom2) ALORS 
      	sListeDifférences += ChaîneConstruit("Prénom2 PC : %1, Prénom2 Outlook : %2",Contact.Prénom2, mContact.Prénom2) + RC
      FIN
      
      // Adresse
      SI (Contact.Adresse 			<> mContact.AdresseDomicile) ALORS 
      	sListeDifférences += ChaîneConstruit("Adresse PC : %1, Adresse Outlook : %2",Contact.Adresse, mContact.AdresseDomicile) + RC
      FIN
      
      // CodePostal
      SI (Contact.CodePostal			<> mContact.CodePostalDomicile) ALORS 
      	sListeDifférences += ChaîneConstruit("Code Postal PC : %1, Code Postal Outlook : %2",Contact.CodePostal, mContact.CodePostalDomicile) + RC
      FIN
      
      // Ville
      SI (Contact.Ville				<> mContact.VilleDomicile) ALORS 
      	sListeDifférences += ChaîneConstruit("Ville PC : %1, Ville Outlook : %2",Contact.Ville, mContact.VilleDomicile) + RC
      FIN
      
      // Etat - Département
      SI (Contact.Etat_Dep				<> mContact.EtatDomicile) ALORS 
      	sListeDifférences += ChaîneConstruit("Etat PC : %1, Etat Outlook : %2",Contact.Etat_Dep, mContact.EtatDomicile) + RC
      FIN
      
      // Pays
      SI (Contact.Pays				<> mContact.PaysDomicile) ALORS 
      	sListeDifférences += ChaîneConstruit("Pays PC : %1, Pays Outlook : %2",Contact.Pays, mContact.PaysDomicile) + RC
      FIN
      
      // Téléphone
      SI (Contact.Téléphone			<> mContact.TéléphoneDomicile) ALORS 
      	sListeDifférences += ChaîneConstruit("Téléphone PC : %1, Téléphone Outlook : %2",Contact.Téléphone, mContact.TéléphoneDomicile) + RC
      FIN
      
      // Mobile
      SI (Contact.Mobile				<> mContact.TéléphoneMobile) ALORS 
      	sListeDifférences += ChaîneConstruit("Mobile PC : %1, Mobile Outlook : %2",Contact.Mobile, mContact.TéléphoneMobile) + RC
      FIN
      
      // Email
      SI (Contact.Email				<> mContact.Email) ALORS 
      	sListeDifférences += ChaîneConstruit("Email PC : %1, Email Outlook : %2",Contact.Email, mContact.Email) + RC
      FIN
      
      // Date de Naissance
      si Contact.datenaissance <> "" et (Contact.DateNaissance <> mContact.DateAnniversaire) alors
      	// Récupère la date d'anniversaire
      	dhDate 	= mContact.DateAnniversaire
      	// Date de Outlook
      	dDate = dhDate..PartieDate
      	si datevalide(dDate) alors
      		sDateOutlookAffichee = DateVersChaîne(dhDate..PartieDate, maskDateSystème)
      	sinon
      		sDateOutlookAffichee = ""
      	fin
      	// Date du PC
      	sDatePCAffichee 	= DateVersChaîne(Contact.DateNaissance, maskDateSystème)
      	sListeDifférences += ChaîneConstruit("DateNaissance PC : %1, DateNaissance Outlook : %2" , sDatePCAffichee , sDateOutlookAffichee) + RC
      FIN
      
      // Date de Fête
      SI Contact.datefete <> "" et (Contact.DateFête <> mContact.DateFête) ALORS 
      	// Récupère la date de fête
      	dhDate 	= mContact.DateFête
      	// Date de Outlook
      	dDate = dhDate..PartieDate
      	si DateValide(dDate) alors
      		sDateOutlookAffichee = DateVersChaîne(dhDate..PartieDate, maskDateSystème)
      	sinon
      		sDateOutlookAffichee = ""
      	fin
      	// Date du PC
      	sDatePCAffichee 	= DateVersChaîne(Contact.datefete, maskDateSystème)
      	sListeDifférences += ChaîneConstruit("DateFête PC : %1, DateFête Outlook : %2" , sDatePCAffichee , sDateOutlookAffichee) + RC
      FIN
      
      // Notes
      SI (Contact.Notes				<> mContact.Notes) ALORS 
      	sListeDifférences += ChaîneConstruit("Notes PC : %1, Notes Outlook : %2",Contact.Notes, mContact.Notes) + RC
      FIN
      
      // On renseigne la liste des différences
      sListe = sListeDifférences
      
      // Est-ce qu'il y a eu des différences ?
      SI pas sListeDifférences~="" alors RENVOYER Vrai
      
      // Par défaut ils sont identiques
      RENVOYER Faux
     type : 458752
   -
     name : CopieContactVersAppli
     internal_properties : CAAAAAgAAACgrIN+k3tOEtvNUD5sRPBvBBbK95SLcpyB/NrJ/JApVSGFK6Alzv3+c7shm6RtWwF4m2bpnilFVdTaTfjuGTJLYihFhaM2GFfP4QIod8DtC5EvMnXbQRTalZf+ZuAjkb1mjkJLmnhA3mM3lsfNgT3jT9u2cu7a3Xfbu7mT+4wUvixXTD0KfXK9eeq70VI8gCY+Okx1uuQz
     procedure_id : 1750049923097110318
     type_code : 15
     code : |1-
      // Résumé : Copie un contact de Outlook du PC vers l'application
      // Syntaxe :
      //CopieContactVersAppli ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      // Traitement automatique des erreurs :   afficher l'erreur, puis réessayer, ignorer ou quitter l'application
      //
      
      Procédure PRIVEE CopieContactVersAppli()
      
      // On renseigne la structure de l'application					
      si OutlookVersLocal() = faux ALORS
      	retour		 
      FIN
      
      // Ajout dans la base de l'application
      HAjoute(Contact)
      
      // Positionne l'ID local dans les notes pour la synchronisation
      Contact.notes = sPositionneIdentifiant(Contact.IDContact, mContact.Notes)
      hmodifie(Contact)
      
      // Met à jour les notes du contact avec l'identification
      mContact.Notes = Contact.notes
      contactModifie(gnIDSession)
     type : 458752
   -
     name : CopieContactVersOutlook
     internal_properties : CAAAAAgAAADJwkw50eQFbrKPH4YatLX3QGEx+Mz8AZOp++45lXA4eKmX5B4g6DSPzBsoHeTX37TZoWXYAI8CZDkS1IIRLrntABLoEUYgAAcBSmURHOHdfVAtvq/6TEleo1kggBU9ynwpt4xksORxhNbQZnNgAxWA0BLKe75Qeo/U2BMNRK6QzZ/SRCxNL6M1T/07uQbyYZuLGI3Q3ElC/Mkd+3DrNxZV71iPgA7FLfz/cpyBKE22UobhXsOOYS/UO2POCyku4v3OVg==
     procedure_id : 1750050451378144994
     type_code : 15
     code : |1+
      // Résumé : Copie un contact de l'application vers Outlook du PC
      // Syntaxe :
      //CopieContactVersOutlook ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      // Traitement automatique des erreurs :   afficher l'erreur, puis réessayer, ignorer ou quitter l'application
      //
      
      Procédure PRIVEE CopieContactVersOutlook()
      
      // On renseigne la structure Outlook
      LocalVersOutlook()	
      
      // Ajout dans la base Outlook
      contactAjoute(gnIDSession)
      
      // Positionne l'ID local dans les notes pour la synchronisation
      Contact.Notes = sPositionneIdentifiant(Contact.IDContact, Contact.Notes)
      // Indique que le contact a été transféré dans Outlook
      Contact.TransféréDansOutlook = Vrai
      HModifie(Contact)
      
      // Met à jour les notes du contact avec l'identification
      mContact.Notes = Contact.Notes
      ContactModifie(gnIDSession)
      
     type : 458752
   -
     name : OutlookVersLocal
     internal_properties : CAAAAAgAAAA6Vf8BSGDB+MxM/eP6KvWeBVx9NwAie3EwHq2J2SYImsTcdNjMpu4VXE08UfJxIxXHeCC6pErp5PZvzJYWRPJOjUQNUfok7hpD8Ep1oeK3fTw+p1o1yXzvToHnPF89z4qy9acKNHQks7S1/XVaBTpLTrDfsy52RYbsJ7uuzeL+G/xCEZLcCD+GTbCvbIXyLODcoYoiit92AiFWIOhPM6lFNCy3QA==
     procedure_id : 1750051349026584935
     type_code : 15
     code : |1-
      // Résumé : Remplit la structure d'un contact de l'application avec les données du contact Outlook du PC
      // Syntaxe :
      //[ <Résultat> = ] OutlookVersLocal ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	booléen : Faux si le contact ne peut pas être ajouté
      //
      
      Procédure PRIVEE OutlookVersLocal()
      
      //Remise à 0 des rubriques pour le Contact, en vue d'une création
      HRAZ(Contact)
      
      //Si le nom ou la société du contact n'ont pas été renseignés, on ne synchronise pas le contact
      si mcontact.nom = "" et mcontact.Société = "" ALORS
      	renvoyer faux
      FIN
      
      //Affectation des rubriques du contact avec les informations du contact de Outlook PC
      Contact.Nom 				= mContact.nom			
      contact.prenom 				= mContact.Prénom			
      Contact.prenom2 			= mContact.Prénom2			
      Contact.Adresse 			= mContact.AdresseDomicile
      Contact.CodePostal			= mContact.CodePostalDomicile
      Contact.Ville				= mContact.VilleDomicile
      Contact.Etat_Dep			= mContact.EtatDomicile
      Contact.Pays				= mContact.PaysDomicile
      Contact.Téléphone			= mContact.TéléphoneDomicile
      Contact.Mobile				= mContact.TéléphoneMobile
      Contact.Email				= mContact.Email
      Contact.DateNaissance		= mContact.DateAnniversaire
      Contact.DateFête			= mContact.DateFête
      Contact.Notes				= mContact.Notes
      
      renvoyer vrai
     type : 458752
   -
     name : LocalVersOutlook
     internal_properties : CAAAAAgAAAAH9Z3WC4edbP8zdcOfnk6o2XwWODs8hLtU0ngzV/wsLAP9SoYrum2SeYXGtBD00SjcPHAsA1EDT0C8ifmF06WO8WRiuEJE3TxUawAQx6CB2WfqZ0hKkRtZrKhy4/cOM7tUOSBbDpPuOVRw+nb2zrb6Fp7UpgZt5fraLnCdKzVOJix5m9lkZg9H0F/5in2tlChy/HP7cFWaQOLQwSo8N2gR2LpgmZcPNzgsC00AHHI2FcNY9A==
     procedure_id : 1750052276739605033
     type_code : 15
     code : |1+
      // Résumé : Remplit la structure d'un contact Outlook avec les données du contact de l'application
      // Syntaxe :
      //LocalVersOutlook ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      
      Procédure PRIVEE LocalVersOutlook()
      
      //Remise à 0 des membres de la structure Contact de Outlook PC
      ContactRAZ()
      
      //Affectation des membres de la structure Contact de Outlook PC avec les données du contact de l'application
      mContact.Nom 				= Contact.Nom
      mContact.Prénom				= Contact.prenom
      mContact.Prénom2			= Contact.Prenom2
      mContact.AdresseDomicile	= Contact.Adresse
      mContact.CodePostalDomicile	= Contact.CodePostal
      mContact.VilleDomicile		= Contact.Ville
      mContact.EtatDomicile		=  Contact.Etat_Dep
      mContact.PaysDomicile		=  Contact.Pays
      mContact.TéléphoneDomicile	= Contact.Téléphone
      mContact.TéléphoneMobile	= Contact.Mobile
      mContact.Email				= Contact.Email
      // Contrôle les dates de naissance et de fête
      si Contact.DateNaissance = "" alors 
      	mContact.DateAnniversaire	= "" 
      SINON	
      	mContact.DateAnniversaire	= Contact.DateNaissance+"000000"
      FIN
      si Contact.DateFête = "" alors
      	mContact.DateFête	= ""		
      SINON
      	mContact.DateFête		= Contact.DateFête+"000000"
      FIN
      mContact.Notes				= Contact.Notes
     type : 458752
   -
     name : GèreContactInexistantOutlook
     internal_properties : CAAAAAgAAABzVi7bl7UF81B4lq2144e2xxOrhTT1EgHJsgNu5FQZ00AAE2zDOwSC/nGquPNke4afvyMRDS4jCWQGXK9UqSzRtOf7K22ptMZ2GDPR47XVpu48bkTE3ox64rBfozLuEWKHM/K/zL5lDOu3ZjBIHwcHLVsO3BEOfCRILik1rbMoQgTXOv7c7UDLBrtKZuw0368Dpvp4PFmmHN/V/+k+X4Qxg4/epcyOqqL0ccyS/t3XjmgXrP7tTZ3FMLw3MEU1ksOSdjVUb0FbgEphbvIf0f/np3b28ICozaLoZcv+Zg==
     procedure_id : 1750052985409243022
     type_code : 15
     code : |1-
      // Résumé : Procédure qui gère le cas où le contact n'existe pas (ou plus) dans Outlook du PC
      // Syntaxe :
      //GèreContactInexistantOutlook ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      
      Procédure PRIVEE GèreContactInexistantOutlook()
      
      // Le contact a déjà été transféré dans Outlook auparavant, il a donc été supprimé de celui-ci
      SI Contact.TransféréDansOutlook ALORS
      	
      	// 1 : Supprimer le contact du PC 
      	// 2 : Restaurer le contact dans Outlook
      	// 3 : Ne rien faire
      	SELON Dialogue(<§?184971AB067BE002§>, contact.nom)
      
      		// Supprimer le contact du PC 
      		CAS 1 :
      			// Supprime le contact de la base locale
      			HSupprime(Contact)
      			
      		// Restaurer le contact dans Outlook
      		CAS 2 :
      			// Ajoute le contact dans la base de Outlook
      			CopieContactVersOutlook()
      			
      		// Ne rien faire
      		CAS 3 :
      		
      	FIN
      	
      SINON 
      	
      	// Le contact n'a jamais été transféré dans Outlook : on l'y ajoute 
      	CopieContactVersOutlook()	
      	
      FIN			
     type : 458752
   -
     name : SynchroniseOutlookVersAppli
     internal_properties : CAAAAAgAAADXsyQwovDYcfxuUWU6rH+PcIIZNKqNuOc4vRlVgbJ6+d9tCLiLUCD+Jooy3kQCu+WRqlfhLlPBPKnV/fMOe0YfFOQZDwfMLX5h2FrCKHsyB5iqVA0isKAiNeHqBKvly45yz+9A0NceTEk2xOgM5ssWeqQUegY9nRD8kWwflC90qfS/4wHw0QeGAwtn5JbMJNcxvIC6W5phFMU+emEbXm54TxPzGqGEqqASoAL7261HmVgV/beD1znBYPV6ncbmxx/IWRDTIlvu0lrYVA==
     procedure_id : 1750055974725002704
     type_code : 15
     code : |1-
      // Résumé : Synchronise les contacts de Outlook du PC vers l'application
      // Syntaxe :
      //SynchroniseOutlookVersAppli ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      // Traitement automatique des erreurs : RENVOYER 
      //
      // Traitement automatique des exceptions :   exécuter le bloc de code CAS EXCEPTION:
      //
      
      Procédure PRIVEE SynchroniseOutlookVersAppli()
      
      nIdContact est un entier	// Identifiant du contact
      
      // Parcours de la base des contacts de Outlook
      contactpremier(gnIDSession)
      
      tantque mContact.EnDehors = faux
      	
      	// Récupère l'identifiant du contact ( s'il en possède un )
      	nIdContact = nRecupereIdentifiant(mContact.notes)
      	
      	// Si un identifiant a été trouvé
      	Si nIdContact <> -1 alors
      		
      		// Recherche l'existence du contact dans la base locale
      		HLitRecherchePremier(Contact, IDContact, nIdContact)
      		
      		// Si le contact existe : il a déjà été synchronisé à l'étape précédente => on ne gère pas le cas ici
      		SI htrouve(Contact) = faux ALORS
      			
      			// Le contact n'existe plus
      			//1 : Supprimer le contact dans Outlook
      			//2 : Restaurer le contact sur le PC 
      			//3 : Ne rien faire
      			SELON Dialogue(<§?1849742A074AA034§>, mContact.nom)
      			
      				// Supprimer le contact de Outlook
      				CAS 1 :
      					contactSupprime(gnIDSession)
      					
      				// Restaurer le contact dans l'application
      				CAS 2 :
      					// Recrée le contact dans la base de l'application
      					CopieContactVersappli()
      					
      				// 	Ne rien faire
      				CAS 3 :
      					
      			FIN
      		FIN
      		
      	SINON
      		
      		// Ajoute le nouveau contact dans la base de l'application
      		CopieContactVersappli()
      		
      	FIN
      	
      	// Lecture du contact suivant
      	contactSuivant(gnIDSession)
      FIN
      
      CAS EXCEPTION:
      Erreur("Erreur lors de la synchronisation des contacts entre Outlook et l'application."+ rc + exceptioninfo())
      RETOUR
     type : 458752
   -
     name : nRecupereIdentifiant
     internal_properties : CAAAAAgAAADa9Z+jsLQxuOTkxWsOksFqFXRhihDaF+26qLE3ExjmiCpGFoyUXnoZTqcgX/gHebst6mL21Hq98MwlnPBwooxw874D+bbYMiKNElofO6CZ/748ahl2OO9oV6qSe5TorNVh1JYdIUf7kFF6OGRPOBOyWYv27jvxzAdtoBoxFqkzzrvBXIEtf6qrZvH2L04BuTOyFxR8CNlgJCcY4to16fNj1ExXIA==
     procedure_id : 1752578696680529313
     type_code : 15
     code : |1-
      // Résumé : Récupère l'identifiant du contact dans les Notes du contact Outlook du PocketPC ou du PC
      // Syntaxe :
      //[ <Résultat> = ] nRecupereIdentifiant (<sChaineNote>)
      //
      // Paramètres :
      //	sChaineNote : Texte des Notes du contact Outlook
      // Valeur de retour :
      // 	entier : Identifiant récupéré s'il a été trouvé, -1 sinon
      //
      
      Procédure PRIVEE nRecupereIdentifiant(sChaineNote)
      
      nPositionTAG	est un entier	// position du TAG identification dans le texte
      nPositionDebut	est un entier	// position de début de l'identifiant dans le texte
      nPositionFin	est un entier	// position de fin de l'identifiant dans le texte
      nIdentifiantTAG	est un entier	// identifiant du TAG
      
      // Le TAG contenant l'identifiant est de la forme  <idBaseInterne:NuméroId>
      // Récupère la position du TAG identification dans le texte
      nPositionTAG = position(sChaineNote , "<idBaseInterne:" , 1 , depuisdebut)
      
      // Si ce TAG existe
      si nPositionTAG <> 0 ALORS
      	
      	// Récupère la position de début de l'identifiant
      	nPositionDebut	= position(sChaineNote , ":" , nPositionTAG , depuisdebut) + 1
      	// Récupère la position de fin de l'identifiant
      	nPositionFin	= position(sChaineNote , ">" ,nPositionDebut , depuisdebut)
      	// Récupère la valeur de l'identifiant
      	nIdentifiantTAG = val( sChaineNote[[ nPositionDebut a nPositionFin-1 ]])
      	// Renvoie l'identifiant trouvé
      	renvoyer nIdentifiantTAG	
      	
      FIN
      
      // L'identifiant n'a pas été trouvé, renvoie -1
      renvoyer -1
     type : 458752
   -
     name : sPositionneIdentifiant
     internal_properties : CAAAAAgAAAD6lb9CUBwtdH7O+cmkLLvkHwpPt7A6dw16aNFXE1jGSMpm9iz0/hr5zifA/7gHGTsNSsKWNBodUMwlvFCwYuxwUx7jmVY4EkKNEvo/O2B5P17cuqmGqF8Y51riCwRYHGURpOatMXcrgCEqiPT/qMPCSZuGPosh/De9kKqBRlnjXqvRrBG97xobdmFG/z5xqQPn9BVpc4Yhp2oXd6vAvpqE9Gx3AA==
     procedure_id : 1752578700975562286
     type_code : 15
     code : |1-
      // Résumé : Crée le TAG d'identification dans les notes d'un contact Outlook PocketPC ou PC
      // Syntaxe :
      //[ <Résultat> = ] sPositionneIdentifiant (<nIdentifiant>, <sNouvellesNotes>)
      //
      // Paramètres :
      //	nIdentifiant : identifiant du contact dans la base locale, à intégrer au TAG
      // 	sNouvellesNotes : <indiquez ici le rôle de sNouvellesNotes>
      // 	sNouvellesNotes : <indiquez ici le rôle de sNouvellesNotes>
      // 	sNouvellesNotes : Notes du contact
      // Valeur de retour :
      // 	Type indéterminé : nouvelle valeur des notes du contact Outlook
      //
      
      Procédure PRIVEE sPositionneIdentifiant(nIdentifiant, sNouvellesNotes)
      
      nPositionTAG	est un entier	// position du TAG identification dans le texte
      nPositionDebut	est un entier	// position de début de l'identifiant dans le texte
      nPositionFin	est un entier	// position de fin de l'identifiant dans le texte
       
      // Le TAG contenant l'identifiant est de la forme  <idBaseInterne:NuméroId>
      // Récupère la position du TAG identification dans le texte
      nPositionTAG = position(sNouvellesNotes, "<idBaseInterne:" , 1 , depuisdebut)
      
      // Si ce TAG existe
      si nPositionTAG <> 0 ALORS
      	
      	// Récupère la position de fin du TAG
      	nPositionFin	= position(sNouvellesNotes , ">" ,nPositionDebut , depuisdebut)+1
      	// Supprime le TAG d'identification (situé en tête des notes)
      	sNouvellesNotes = droite(sNouvellesNotes , taille(sNouvellesNotes)-nPositionFin)
      	
      FIN
      // Positionne le nouveau TAG d'identification dans le texte des notes
      sNouvellesNotes = "<idBaseInterne:"+nIdentifiant+">" + rc + sNouvellesNotes	
      
      // Renvoie la nouvelle valeur des notes
      renvoyer sNouvellesNotes
     type : 458752
   -
     name : SynchroniseContactsOutlook
     internal_properties : CAAAAAgAAAAmMAoP1woFAt8QeRvAt2qRXKKTojTSksjrbZnsliZq/wXGvFZLleojLm/YPH3XPFzt2wQruvCoLC/hJRs3cHjPUcvTGkmIOu/EII7heRGPWObAnL7vMmZZ5ybtUq3H2icQX7pHMRWluQfr/Dxta88z/ZAtxuUUGmzKcxzN1iQVX24W+wQ6TAW+wTPc13dlEI501vDBKdR3Tm5BmVz/9hfi2WrnQyRAstAiTH0UxdDKEaPZOunIj4iyyJ8G6ys2tBmiV/hyx3VhWyYR0+krT0PQaK18sXU=
     procedure_id : 1829044679587401221
     type_code : 15
     code : |1-
      // Résumé : Synchronise les contacts entre l'application et Outlook du PC
      // Syntaxe :
      //SynchroniseContactsOutlook ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      
      Procédure restreinte SynchroniseContactsOutlook()
      
      //Ouverture d'une session Outlook
      gnIDSession = OutlookOuvreSession("")
      
      fRepEnCours(ComplèteRep(fRepExe()))
      
      si gnIDSession = 0 alors
      	erreur("Impossible d'accéder aux données de la messagerie Outlook. Cet exemple fonctionne avec Microsoft Outlook, non Outlook Express.") 
      	retour
      FIN
      
      //Affiche un sablier d'attente
      sablier(vrai)
      
      //Synchronise les contacts de l'application vers Outlook
      SynchroniseAppliVersOutlook()
      
      //Synchronise les contacts de Outlook vers l'application
      SynchroniseOutlookVersAppli()
      
      //Stoppe l'affichage du sablier
      sablier(faux)
      
      //Informe l'utilisateur de la fin de la synchronisation Outlook - PC
      info("Synchronisation des contacts terminée.")
     type : 458752
   -
     name : SynchroniseAppliVersOutlook
     internal_properties : CAAAAAgAAAAc2u8/6fD48RMACWz6Fhn6joIiqNQouUbgJTEUPQlX8xBpRawm1+ayNVKKFFtp+0ICRnBCZ0fYP36Puli+cpFMxdl/agFP0ZpFbbHB576lemeIdyVKtwT+xFn/bOlmrjjHk0OtrncbH8wWkbH5HA1jfVi10B7zvu4IhLPsKj/7uToYlSgd49+cK+rdRMfqZCaMrWdgL04mgV29s2j6wr7XSHzNd7AkEbgCC1Bp6Bm+VZ2NrNE/GCoGDRl4xAaR6w==
     procedure_id : 1829826621273999881
     type_code : 15
     code : |1+
      // Résumé : Synchronise les contacts de l'application vers Outlook du PC
      // Syntaxe :
      //SynchroniseAppliVersOutlook ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      
      Procédure PRIVee SynchroniseAppliVersOutlook()
      
      //Parcours de la base locale
      Pour TOUT Contact 
      	
      	//Vérifie que le contact existe dans Outlook
      	Si bRechercheContactOutlook(contact.IDContact) = vrai alors
      		
      		//S'il existe dans Outlook, on gère les modifications du contact entre la base de l'application et Outlook
      		GèreModifContact_PC_Outlook()
      	
      	sinon
      		
      		//Le contact n'existe pas (ou plus) dans Outlook : on gère ce cas 
      		GèreContactInexistantOutlook()
      		
      	FIN
      FIN
      
     type : 458752
   -
     name : bRechercheContactOutlook
     internal_properties : CAAAAAgAAAC+f6VfjFI3qwPeDC4nGuO1YcVAUJsTSp1FqfTxG3Gr0v3a+2EI34PnIpvElCIsTS13zFNTrZZc+Z8TE+KBOkv8J/LKK6bu+u61gGWfUy1nYgPjqZAab4Ged81zeg7n+P6x0lP0V1ix2sHjURtLdx/ppxunt+d46sFN0a2SGN7XLcGhoQZRTBh1WoDHMgY03xqPcvYO19nLTbpAQqQYn6MUKXH9
     procedure_id : 1832406805836245927
     type_code : 15
     code : |1-
      // Résumé : Recherche l'existence de l'identifiant passé en paramètre parmi les contacts de Outlook du PC
      // Syntaxe :
      //[ <Résultat> = ] bRechercheContactOutlook (<nIdentifiant>)
      //
      // Paramètres :
      //	nIdentifiant : identifiant à rechercher
      // Valeur de retour :
      // 	booléen : VRAI si l'identifiant a été trouvé, FAUX autrement
      //
      
      PROCEDURE PRIVÉE bRechercheContactOutlook(nIdentifiant)
      
      // Parcours des contacts de Outlook
      ContactPremier(gnIDSession)
      
      TANTQUE mContact.EnDehors = Faux
      	
      	// Si l'identifiant correspond
      	SI nRecupereIdentifiant(mContact.Notes) = nIdentifiant ALORS
      		// L'identifiant a été trouvé
      		RENVOYER Vrai
      	FIN
      	
      	ContactSuivant(gnIDSession)
      FIN
      
      // L'identifiant n'a pas été trouvé
      RENVOYER Faux
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
  original_name : COL_Collection1
resources :
 string_res :
  identifier : 0x1849627604617887
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
 dialogs :
  -
    identifier : 0x18496c7f054cab6a
    internal_properties : CAAAAAgAAAA9SBiBNRj6LRp0eUb879IXCRLpNp5LLvHaSC5MCC4jiD2oujY2u8yJtmSq1vjmhvQ8JNKdeMPqHzngmxiavECnbNC2qo4=
    label :
     fr-FR : "Les informations du contact '%1' différent entre l'application et Outlook.\r\n\r\nListe des différences :\r\n%2\r\n\r\nQue souhaitez-vous faire ?"
    image : {}
    cancel_button : 2
    buttons :
     -
       label :
        fr-FR : Conserver les valeurs de Outlook
       image : {}
       value : 1
     -
       label :
        fr-FR : Conserver les valeurs de l'application
       image : {}
       value : 2
     -
       label :
        fr-FR : Ne rien faire
       image : {}
       value : 3
  -
    identifier : 0x184971ab067be002
    internal_properties : CAAAAAgAAADteEhRZSjqPWpkCVbMvwIn+cIZxs4bXsHaKA5sCG4DKL0oKkZGy1wZBlQ65ugW1kTsVIJtqJO6b0nwqwiKrFC3HABmer4=
    label :
     fr-FR : "Le contact '%1' a été supprimé de la liste des contacts de Outlook.\r\n\r\nQue souhaitez-vous faire ?"
    image : {}
    cancel_button : 2
    buttons :
     -
       label :
        fr-FR : Supprimer le contact du PC
       image : {}
       value : 1
     -
       label :
        fr-FR : Restaurer le contact dans Outlook
       image : {}
       value : 2
     -
       label :
        fr-FR : Ne rien faire
       image : {}
       value : 3
  -
    identifier : 0x1849742a074aa034
    internal_properties : CAAAAAgAAABh/DQXeQiOjdaUler4O8ZzJeq5SlqLasmG4GLghJKPXAFIdFD02f6nqPYoSG6cSDb+bvwnLknkDSNqAXLQUiKRRjTi/jo=
    label :
     fr-FR : "Le contact '%1' a été supprimé de la liste des contacts de l'application.\r\n\r\nQue souhaitez-vous faire ?\r\n"
    image : {}
    cancel_button : 2
    buttons :
     -
       label :
        fr-FR : Supprimer le contact dans Outlook
       image : {}
       value : 1
     -
       label :
        fr-FR : "Restaurer le contact sur le PC "
       image : {}
       value : 2
     -
       label :
        fr-FR : Ne rien faire
       image : {}
       value : 3
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
